plugins {
    id "java"
    id "idea"
    id "eclipse"
    id "application"
}

version = "0.0.0"
group = "net.gegy1000"
archivesBaseName = "slyther"
mainClassName = "net.gegy1000.slyther.client.ClientMain"
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

task createServerStartScript(type: CreateStartScripts) {
  mainClassName = 'net.gegy1000.slyther.server.ServerMain'
  applicationName = 'slyther-server'
  classpath = startScripts.classpath
  outputDir = startScripts.outputDir
}

applicationDistribution.into("bin") {
    duplicatesStrategy= DuplicatesStrategy.EXCLUDE
    from(createServerStartScript)
    fileMode = 0755
}

repositories {
    mavenCentral()
}

configurations {
    natives
}

dependencies {
	compile "org.slick2d:slick2d-core:1.0.1"
    compile "org.lwjgl.lwjgl:lwjgl:2.9.3"
    compile "commons-io:commons-io:2.4"
    compile "com.google.code.gson:gson:1.7.2"
    compile "org.apache.logging.log4j:log4j-api:2.5"
    compile "org.apache.logging.log4j:log4j-core:2.5"
    natives "org.lwjgl.lwjgl:lwjgl-platform:2.9.3:natives-windows"
    natives "org.lwjgl.lwjgl:lwjgl-platform:2.9.3:natives-osx"
    natives "org.lwjgl.lwjgl:lwjgl-platform:2.9.3:natives-linux"
    natives "net.java.jinput:jinput-platform:2.0.5:natives-windows"
    natives "net.java.jinput:jinput-platform:2.0.5:natives-osx"
    natives "net.java.jinput:jinput-platform:2.0.5:natives-linux"
    compile "org.reflections:reflections:0.9.10"
}

jar {
    manifest {
        attributes("Main-Class": project.mainClassName)
    }
}

processResources {
    configurations.compile.each {
        from (zipTree(it)) {
            exclude "**/META-INF/"
        }
    }
    // native processing will probably change
    configurations.natives.each {
        from (zipTree(it)) {
            exclude "**/META-INF/"
        }
    }
}
